<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" > 

<title>Draw Square</title>

<script id="vertex-shader" type="x-shader/x-vertex">
precision mediump float;

attribute vec4 vPosition;
varying vec3 fPosition;

void main(){
  gl_Position = vPosition;
  fPosition = vPosition.xyz;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
#define PI 3.14159265358979
precision mediump float;

varying vec3 fPosition;
uniform float time;
uniform sampler2D diffuseMapSampler;

struct Ray {
  vec3 origin;
  vec3 direction;   // should always be normalized
};

struct Sphere {
  vec3 center;
  float radius;
  int material_id;  //index of material list
};

struct IntersectedObject{
  Sphere sphere;
  vec3 point;       // the point where a ray intersects the sphere
  float distance;   // the distance from the ray origin to the spere. -1.0 if no intersection.
};

struct Light {
  vec3 position;
  float intensity;
  vec3 color;
};

struct Material {
  vec3 color;
  float reflectivity;
  float shininess;
};

Sphere sphereList[4];
Light lightList[2];
Material materialList[4];

vec3 ambient_light = vec3(0.5, 0.5, 0.5);
vec3 background_color = vec3(0.1, 0.1, 0.1);
vec3 cameraPos = vec3(0.0, 0.0, 5.0);

const int trace_depth = 3; //how many reflection rays we want to consider

/**
 * Used inside hit function. 
 * Tests whether a ray hits the given sphere
 * 
 * @param  {Ray}     ray
 * @param  {Sphere}  sphere
 * @return {float}        distance from ray origin to the sphere
 */
float intersect(Ray ray, Sphere sphere) {
  float distance, t1, t2;
  vec3 x = ray.origin - sphere.center;
  float A = dot(ray.direction, ray.direction);
  float B = 2.0 * dot(x, ray.direction);
  float C = dot(x,x) - sphere.radius * sphere.radius;
  float D = B*B - 4.0*A*C;
  distance = 99999.99;
  if (D >= 0.0){
    float S = sqrt(D);
    t1 = (-B - S)/(2.0 * A);
    t2 = (-B + S)/(2.0 * A);
    if (t1 >= 0.0){
      distance = t1;
    }
    if (t2 >= 0.0 && t2 < t1 ){
      distance = t2;
    }
  } else {
    distance = -1.0; //return -1.0 if there are no intersections
  }
  return distance;
}

/**
 * Finds the closest object that the given ray hits.
 * 
 * @param  {Ray} ray           ray with origin and direction
 * @return {hitObject}     object that is closest to the ray origin, 
 *                         the point where the ray hits, 
 *                         and how far the point is
 */
IntersectedObject hit(Ray ray) {
  float distanceToTheClosestObject = 9999999.0;
  Sphere tempSphere = Sphere(vec3(0.0, 0.0, 0.0), 0.1, 0);
  vec3 tempPoint = vec3(0.0, 0.0, 0.0);
  IntersectedObject object = IntersectedObject(tempSphere, tempPoint, -1.0); // set the distance to -1.0 (default)

  for (int i = 0; i < 4; i++) {
    float distanceToObject = intersect(ray, sphereList[i]);
    if (distanceToObject > 0.0 && distanceToObject < distanceToTheClosestObject) {
      object.sphere = sphereList[i];
      object.distance = distanceToObject;
      object.point = vec3(ray.origin + distanceToObject * ray.direction);

      distanceToTheClosestObject = distanceToObject;
    }
  }

  return object;
}

/**
 * Calculates what color a given point (on an object) is
 * 
 * @param  {vec3}      position      position in 3D of the position where the ray hit the object
 * @param  {vec3}      normal        normal calculated at given position
 * @param  {vec3}      viewDir       viewing direction from camera to object
 * @param  {Material}  material      material of object at given position
 * @return {vec3}          color of object at given position
 */
vec3 shade(vec3 position, vec3 normal, vec3 viewDir, Material material) {
  Light l;
  vec3 toLight, lightDir;
  vec3 halfvector;
  float shininess = material.shininess;
  vec3 color = material.color;
  vec3 diffuse = ambient_light;
  vec3 final_color = vec3(0.1, 0.1, 0.0); //default color of balls
  vec3 specular = vec3(0.0, 0.2, 0.0);

  //if material id is 0, then we are going to use the normal to calculate the color from billiard ball texture.
  if (material == materialList[0]) {
    vec2 textureCoordinates = vec2(0.0, 0.0);
    float x = normal.x;
    float y = normal.y;
    float z = normal.z;

    //transform cartesian to spherical coordinates
    float theta = atan(y/x);
    float phi = atan(sqrt(x*x + y*y)/z);
    textureCoordinates.x = (theta / (2.0 * PI));
    textureCoordinates.y = (phi + PI/2.0) / PI;

    final_color = texture2D(diffuseMapSampler, textureCoordinates).rgb;
  }

  for (int i = 0; i < 2; i++) {
    l = lightList[i];
    toLight = l.position - position;
    lightDir = normalize(toLight);

    Ray shadowRay = Ray(position, lightDir);

    if (hit(shadowRay).distance > length(toLight)) {
      float diffuseIntensity = l.intensity * max(dot(lightDir, normal), 0.0);
      diffuse = diffuse + diffuseIntensity * l.color;
      halfvector = normalize(viewDir + lightDir);
      float specularIntensity = pow(max(dot(halfvector, normal), 0.0), float(shininess));
      specular = specular + specularIntensity * l.color;
    }
  }

  final_color = final_color + diffuse * color;
  final_color = final_color + specular;

  return final_color;
}

/**
 * Iteratively traces given ray and returns a color. 
 * trace_depth is defined above.
 * 
 * @param  {Ray}  ray        the ray that is being traced
 * @return {vec3}            calculated color from given ray
 */
vec3 trace(Ray ray) {
  vec3 final_color = vec3(0.0, 0.0, 0.0);
  bool no_hit = true;
  float r = 0.5;
  Material material;

  Ray hitRay = ray;

  for (int i = 0; i < trace_depth; i++) {
    IntersectedObject intersectedObject = hit(hitRay);

    if (intersectedObject.distance > 0.0) {
      no_hit = false;
      vec3 normal = normalize(intersectedObject.point - intersectedObject.sphere.center);
      vec3 viewDir = -1.0 * hitRay.direction;
      int material_id = intersectedObject.sphere.material_id;

      if (material_id == 0) {
        material = materialList[0];
      } else if (material_id == 1) {
        material = materialList[1];
      } else if (material_id == 2) {
        material = materialList[2];
      } else if (material_id == 3) {
        material = materialList[3];
      }

      vec3 color = shade(intersectedObject.point, normal, viewDir, material);
      vec3 reflected_dir = hitRay.direction + (-2.0 * dot(hitRay.direction, normal) * normal);
      hitRay = Ray(intersectedObject.point, reflected_dir);
      r *= material.reflectivity;
      final_color = final_color + r * color;
    }
  }

  if (no_hit) {
    return background_color;
  } else {
    return final_color;
  }
}

/**
 * Calculated direction of ray shot from camera towards each fragment
 * @param  {float} float x             x coordinate of fragment or screen
 * @param  {float} float y             y coordinate of fragment or screen
 * @return {Ray}       ray shot from camera towards each fragment
 */
Ray pixelRay(float x, float y){
  vec3 d = vec3(vec3(x, y, 0.0) - cameraPos);
  // in honor of our dear professor Saurabh Ray!
  Ray Saurabh = Ray(cameraPos, normalize(d));
  return Saurabh;
}


/**
 * Pseudo-random number generator from -0.001 to 0.001 found on the Internet
 * 
 * @param  {vec2}       seed  two numbers as seed for hash
 * @return {float}      a number that is quite unpredictable
 */
float hash(vec2 seed){
  float A2 = 42420.1319;
  float t = 12.9898*seed.x + 78.233*seed.y;
  return fract((A2+t) * sin(t)) / 500.0;
}

/**
 * Perform anti-aliasing by tracing 4 random rays and averaging them out
 * 
 * @param  {vec3}   fPosition     position of fragment
 * @param  {float}  time          time as seed for randomization
 * @return {vec4}       the averaged color from 4 traces 
 */
vec4 antiAlias(vec3 fPosition, float time){
  vec4 averageColor = vec4(0.0, 0.0, 0.0, 0.0);

  float hashNumber1 = hash(vec2(fPosition.x, time));
  float hashNumber2 = hash(vec2(fPosition.y, time));
  float hashNumber3 = hash(vec2(fPosition.z, time));
  float hashNumber4 = hash(vec2(fPosition.x, fPosition.y));

  Ray ray1 = pixelRay(fPosition.x + hashNumber1, fPosition.y + hashNumber1);
  Ray ray2 = pixelRay(fPosition.x + hashNumber2, fPosition.y - hashNumber2);
  Ray ray3 = pixelRay(fPosition.x - hashNumber3, fPosition.y + hashNumber3);
  Ray ray4 = pixelRay(fPosition.x - hashNumber4, fPosition.y - hashNumber4);

  averageColor += vec4(trace(ray1), 1.0);
  averageColor += vec4(trace(ray2), 1.0);
  averageColor += vec4(trace(ray3), 1.0);
  averageColor += vec4(trace(ray4), 1.0);
  averageColor /= 4.0;

  return averageColor;
}

void main(){

  sphereList[0] = Sphere(vec3(0.0, 0.0, 0.0), 0.4, 0);
  sphereList[1] = Sphere(vec3(0.0 + cos(time)/1.25, -0.2, 0.0 + sin(time))/1.25, 0.15, 1);
  sphereList[2] = Sphere(vec3(-0.5, 0.5, 0.0 + sin(time*3.0)), 0.2, 2);
  sphereList[3] = Sphere(vec3(0.0 + sin(time), 0.0 + cos(time), 0.0), 0.2 + sin(time)/10.0, 3);

  lightList[0] = Light(vec3(2.0, 2.0, 3.0), 5.0, vec3(1.0, 1.0, 1.0));
  lightList[1] = Light(vec3(0.0 + cos(time), 1.0, 0.0 + sin(time)), 5.0, vec3(1.0, 1.0, 1.0));

  materialList[0] = Material(vec3(0.4, 0.4, 0.6), 0.3, 1000.0);
  materialList[1] = Material(vec3(0.13, 0.32, 0.47), 0.3, 100.0);
  materialList[2] = Material(vec3(0.92, 0.49, 0.0), 0.2, 100.0);
  materialList[3] = Material(vec3(0.95, 0.10, 0.88), 0.2, 1000.0);
  
  gl_FragColor = antiAlias(fPosition, time);
}

</script>

<script type="text/javascript" src="../../common/webgl-utils.js"></script>
<script type="text/javascript" src="../../common/initShaders.js"></script>
<script type="text/javascript" src="../../common/MV.js"></script>
<script type="text/javascript" src="RayTracer.js"></script>
</head>

<body>
<canvas id="gl-canvas" width="512" height="512">
  HTML5 Canvas not supported!
</canvas>
</body>
</html>

