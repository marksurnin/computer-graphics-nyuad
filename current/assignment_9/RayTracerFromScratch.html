<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" > 

<title>Draw Square</title>

<script id="vertex-shader" type="x-shader/x-vertex">
precision mediump float;

attribute vec4 vPosition;
varying vec3 fPosition;

void main(){
  gl_Position = vPosition;
  fPosition = vPosition.xyz;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
#define PI = 3.14;
precision mediump float;

varying vec3 fPosition;
uniform float time;
uniform sampler2D diffuseMapSampler;

struct Ray {
  vec3 origin;
  // should always be normalized
  vec3 direction;
};

struct Sphere {
  vec3 center;
  float radius;
  int material_id;
};

struct IntersectedObject{
  Sphere sphere;
  // the point where a ray intersects the sphere
  vec3 point;
  // the distance from the ray origin to the spere. -1.0 if no intersection.
  float distance;
};

struct Light {
  vec3 position;
  float intensity;
  vec3 color;
};

struct Material {
  vec3 color;
  float reflectivity;
  float shininess;
};


Sphere sphereList[2];
Light lightList[2];
Material materialList[2];

vec3 ambient_light = vec3(0.5, 0.5, 0.5);
vec3 background_color = vec3(0.1, 0.1, 0.1);
vec3 cameraPos = vec3(0.0, 0.0, 5.0);

const int trace_depth = 3;

float intersect(Ray ray, Sphere sphere) {
  float distance, t1, t2; //
  vec3 x = ray.origin - sphere.center; // vec from sphere center to ray origin
  float A = dot(ray.direction, ray.direction); //size^2
  float B = 2.0 * dot(x, ray.direction); // 
  float C = dot(x,x) - sphere.radius * sphere.radius;
  float D = B*B - 4.0*A*C; //b^2 -4ac
  distance = 99999.99;
  if (D >= 0.0){
    float S = sqrt(D); //sqrt(b^2-4ac)
    t1 = (-B - S)/(2.0 * A);
    t2 = (-B + S)/(2.0 * A);
    if (t1 >= 0.0){
      distance = t1;
    }
    if (t2 >= 0.0 && t2 < t1 ){
      distance = t2;
    }
  } else {
    distance = -1.0;
  }
  return distance;
}

IntersectedObject hit(Ray ray) {
  float distanceToTheClosestObject = 9999999.0;
  Sphere tempSphere = Sphere(vec3(0.0, 0.0, 0.0), 0.1, 0);
  vec3 tempPoint = vec3(0.0, 0.0, 0.0);
  // set the distance to -1.0 (default)
  IntersectedObject object = IntersectedObject(tempSphere, tempPoint, -1.0);

  for (int i = 0; i < 2; i++) {
    float distanceToObject = intersect(ray, sphereList[i]);
    if (distanceToObject > 0.0 && distanceToObject < distanceToTheClosestObject) {
      object.sphere = sphereList[i];
      object.distance = distanceToObject;
      object.point = vec3(ray.origin + distanceToObject * ray.direction);

      distanceToTheClosestObject = distanceToObject;
    }
  }

  return object;
}

vec3 shade(vec3 position, vec3 normal, vec3 viewDir, Material material) {
  Light l;
  vec3 toLight, lightDir;
  vec3 halfvector;
  float shininess = material.shininess;
  vec3 color = material.color;
  vec3 diffuse = ambient_light;
  vec3 final_color = vec3(0.1, 0.1, 0.0); //default color of balls
  vec3 specular = vec3(0.0, 0.2, 0.0);

  //if material id is something, then we are going to use the normal to calculate the color from billiard ball texture.
  if (material == materialList[0]) {
    vec2 textureCoordinates = vec2(0.0, 0.0);
    float x = normal.x;
    float y = normal.y;
    float z = normal.z;

    //transform cartesian to spherical coordinate
    float theta = atan(y/x);
    float phi = atan(sqrt(x*x + y*y)/z);
    // textureCoordinates.x = (theta / (2.0 * PI));
    textureCoordinates.x = (theta / (2.0 * 3.14));
    // textureCoordinates.x = 3.14;
    // textureCoordinates.x = 0.1;
    textureCoordinates.y = (phi + 3.14/2.0) / 3.14;
    // textureCoordinates = vec2((theta / (2.0 * PI)), (phi + PI/2.0) / PI);
    final_color = texture2D(diffuseMapSampler, textureCoordinates).xyz;
  }

  for (int i = 0; i < 2; i++) {
    l = lightList[i];
    toLight = l.position - position;
    lightDir = normalize(toLight);

    Ray shadowRay = Ray(position, lightDir);

    if (hit(shadowRay).distance > length(toLight)) {
      float diffuseIntensity = l.intensity * max(dot(lightDir, normal), 0.0);
      diffuse = diffuse + diffuseIntensity * l.color;
      halfvector = normalize(viewDir + lightDir);
      float specularIntensity = pow(max(dot(halfvector, normal), 0.0), float(shininess));
      specular = specular + specularIntensity * l.color;
    }
  }

  final_color = final_color + diffuse * color;
  final_color = final_color + specular;

  return final_color;
}

vec3 trace(Ray ray) {
  vec3 final_color = vec3(0.0, 0.0, 0.0);
  bool no_hit = true;
  float r = 0.5;
  Material material;

  Ray hitRay = ray;

  for (int i = 0; i < trace_depth; i++) {
    IntersectedObject intersectedObject = hit(hitRay);

    if (intersectedObject.distance > 0.0) {
      no_hit = false;
      vec3 normal = normalize(intersectedObject.point - intersectedObject.sphere.center);
      vec3 viewDir = -1.0 * hitRay.direction;
      int material_id = intersectedObject.sphere.material_id;

      if (material_id == 0) {
        material = materialList[0];
      } else if (material_id == 1) {
        material = materialList[1];
      }

      vec3 color = shade(intersectedObject.point, normal, viewDir, material);
      vec3 reflected_dir = hitRay.direction + (-2.0 * dot(hitRay.direction, normal) * normal);
      hitRay = Ray(intersectedObject.point, reflected_dir);
      r *= material.reflectivity;
      final_color = final_color + r * color;
    }
  }

  if (no_hit) {
    return background_color;
  } else {
    return final_color;
  }
}

Ray pixelRay(float x, float y){
  vec3 d = vec3(vec3(x, y, 0.0) - cameraPos);
  Ray ray = Ray(cameraPos, normalize(d));
  return ray;
}

//Pseudo-random number generator from -0.001 to 0.001
float hash(vec2 co){
  float A2 = 42420.1319;
  float t = 12.9898*co.x + 78.233*co.y; 
  return fract((A2+t) * sin(t)) / 500.0;  // any B2 is folded into 't' computation
}

// Anti-aliasing: trace color for 4 points around given color and average
vec4 antiAlias(vec3 fPosition, float time){
  vec4 averageColor = vec4(0.0, 0.0, 0.0, 0.0);

  float hashNumber1 = hash(vec2(fPosition.x, time));
  float hashNumber2 = hash(vec2(fPosition.y, time));
  float hashNumber3 = hash(vec2(fPosition.z, time));
  float hashNumber4 = hash(vec2(fPosition.x, fPosition.y));

  Ray ray1 = pixelRay(fPosition.x + hashNumber1, fPosition.y + hashNumber1);
  Ray ray2 = pixelRay(fPosition.x + hashNumber2, fPosition.y - hashNumber2);
  Ray ray3 = pixelRay(fPosition.x - hashNumber3, fPosition.y + hashNumber3);
  Ray ray4 = pixelRay(fPosition.x - hashNumber4, fPosition.y - hashNumber4);

  averageColor += vec4(trace(ray1), 1.0);
  averageColor += vec4(trace(ray2), 1.0);
  averageColor += vec4(trace(ray3), 1.0);
  averageColor += vec4(trace(ray4), 1.0);
  averageColor /= 4.0;

  return averageColor;
}

void main(){

  sphereList[0] = Sphere(vec3(0.0, 0.0, 0.0), 0.4, 1);
  sphereList[1] = Sphere(vec3(0.0 + cos(time)/1.25, -0.2, 0.0 + sin(time))/1.25, 0.15, 0);
  // sphereList[2] = Sphere(vec3(-0.5, 0.0, 0.0 + sin(time)), 0.2, 1);

  // lightList[0] = Light(vec3((0.3 + cos(time))/2.0, 0.3, (sin(time) - 0.1)/2.0), 5.0, vec3(1.0, 0.5, 0.5));
  lightList[0] = Light(vec3(2.0, 2.0, 3.0), 5.0, vec3(1.0, 1.0, 1.0));
  lightList[1] = Light(vec3(0.0, 0.5, 2.0), 5.0, vec3(1.0, 0.5, 0.5));

  materialList[0] = Material(vec3(0.0, 0.2, 0.4), 1.0, 1000.0);
  materialList[1] = Material(vec3(0.0, 0.2, 0.0), 0.8, 1000.0);  

  
  gl_FragColor = antiAlias(fPosition, time);
}

</script>

<script type="text/javascript" src="../../common/webgl-utils.js"></script>
<script type="text/javascript" src="../../common/initShaders.js"></script>
<script type="text/javascript" src="../../common/MV.js"></script>
<script type="text/javascript" src="RayTracerFromScratch.js"></script>
</head>

<body>
<canvas id="gl-canvas" width="512" height="512">
  HTML5 Canvas not supported!
</canvas>
</body>
</html>

