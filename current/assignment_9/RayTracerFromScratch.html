<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" > 

<title>Draw Square</title>

<script id="vertex-shader" type="x-shader/x-vertex">
precision mediump float;

attribute vec4 vPosition;
varying vec3 fPosition;

void main(){
  gl_Position = vPosition;
  fPosition = vPosition.xyz;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
precision mediump float;

varying vec3 fPosition;

struct Ray {
  vec3 origin;
  // should always be normalized
  vec3 direction;
};

struct Sphere {
  vec3 center;
  float radius;
  int material_id;
};

struct IntersectedObject{
  Sphere sphere;
  // the point where a ray intersects the sphere
  vec3 point;
  // the distance from the ray origin to the spere. -1.0 if no intersection.
  float distance;
};

struct Light {
  vec3 position;
  float intensity;
  vec3 color;
};

struct Material {
  vec3 color;
  float reflectivity;
  float shininess;
};

Sphere sphereList[2];
Light lightList[1];
Material materialList[2];

vec3 ambient_light = vec3(0.5, 0.5, 0.5);
vec3 background_color = vec3(0.0, 0.2, 0.0);

const int trace_depth = 1;

float intersect(Ray ray, Sphere sphere) {
  float distance, t1, t2; //
  vec3 x = ray.origin - sphere.center; // vec from sphere center to ray origin
  float A = dot(ray.direction, ray.direction); //size^2
  float B = 2.0 * dot(x, ray.direction); // 
  float C = dot(x,x) - sphere.radius * sphere.radius;
  float D = B*B - 4.0*A*C; //b^2 -4ac
  distance = 99999.99;
  if (D >= 0.0){
    float S = sqrt(D); //sqrt(b^2-4ac)
    t1 = (-B - S)/(2.0 * A);
    t2 = (-B + S)/(2.0 * A);
    if (t1 >= 0.0){
      distance = t1;
    }
    if (t2 >= 0.0 && t2 < t1 ){
      distance = t2;
    }
  } else {
    distance = -1.0;
  }
  return distance;
}

IntersectedObject hit(Ray ray) {
  float distanceToTheClosestObject = 9999999.0;
  Sphere tempSphere = Sphere(vec3(0.0, 0.0, 0.0), 0.1, 0);
  vec3 tempPoint = vec3(0.0, 0.0, 0.0);
  // set the distance to -1.0 (default)
  IntersectedObject object = IntersectedObject(tempSphere, tempPoint, -1.0);

  for (int i = 0; i < 2; i++) {
    float distanceToObject = intersect(ray, sphereList[i]);
    if (distanceToObject > 0.0 && distanceToObject < distanceToTheClosestObject) {
      object.sphere = sphereList[i];
      object.distance = distanceToObject;
      object.point = vec3(ray.origin + distanceToObject * ray.direction);

      distanceToTheClosestObject = distanceToObject;
    }
  }

  return object;
}

vec3 shade(vec3 position, vec3 normal, vec3 viewDir, Material material) {
  Light l;
  vec3 toLight, lightDir;
  vec3 halfvector;
  float shininess = material.shininess;
  vec3 color = material.color;
  vec3 diffuse = ambient_light;
  vec3 final_color = vec3(0.0, 0.0, 0.0);
  vec3 specular = vec3(0.0, 0.2, 0.0);

  for (int i = 0; i < 2; i++) {
    l = lightList[i];
    toLight = l.position - position;
    lightDir = normalize(toLight);

    Ray shadowRay = Ray(position, lightDir);

    if (hit(shadowRay).distance > length(toLight)) {
      float diffuseIntensity = l.intensity * max(dot(lightDir, normal), 0.0);
      diffuse = diffuse + diffuseIntensity * l.color;
      halfvector = normalize(viewDir + lightDir);
      float specularIntensity = pow(max(dot(halfvector, normal), 0.0), float(shininess));
      specular = specular + specularIntensity * l.color;
    }
  }

  final_color = final_color + diffuse * color;
  final_color = final_color + specular;

  return final_color;
}

vec3 trace(Ray ray) {
  vec3 final_color = vec3(0.0, 0.0, 0.0);
  bool no_hit = true;
  float r = 0.5;
  Material material;

  for (int i = 0; i < trace_depth; i++) {
    IntersectedObject intersectedObject = hit(ray);

    if (intersectedObject.distance > 0.0) {
      no_hit = false;
      vec3 normal = normalize(intersectedObject.point - intersectedObject.sphere.center);
      vec3 viewDir = -1.0 * ray.direction;
      int material_id = intersectedObject.sphere.material_id;

      if (material_id == 0) {
        material = materialList[0];
      } else if (material_id == 1) {
        material = materialList[1];
      }

      vec3 color = shade(intersectedObject.point, normal, viewDir, material);
      vec3 reflected_dir = ray.direction + (-2.0 * dot(ray.direction, normal) * normal);
      Ray ray = Ray(intersectedObject.point, reflected_dir);
      final_color = final_color + r * color;
      r *= material.reflectivity;
    }
  }

  if (no_hit) {
    return background_color;
  } else {
    return final_color;
  }
}

void main(){
  vec3 cameraPos = vec3(0.0, 0.0, 5.0);
  vec3 d = vec3(vec3(fPosition.xy, 0.0) - cameraPos);
  Ray ray = Ray(cameraPos, normalize(d));

  sphereList[0] = Sphere(vec3(0.0, 0.0, 0.0), 0.3, 0);
  sphereList[1] = Sphere(vec3(0.3, -0.1, 0.5), 0.1, 1);

  lightList[0] = Light(vec3(1.0, 0.0, 5.0), 5.0, vec3(1.0, 0.5, 0.5));

  materialList[0] = Material(vec3(0.0, 0.2, 0.4), 0.2, 100.0);
  materialList[1] = Material(vec3(0.0, 0.2, 0.0), 0.8, 100.0);  

  gl_FragColor = vec4(trace(ray), 1.0);
}

</script>

<script type="text/javascript" src="../../common/webgl-utils.js"></script>
<script type="text/javascript" src="../../common/initShaders.js"></script>
<script type="text/javascript" src="../../common/MV.js"></script>
<script type="text/javascript" src="RayTracerFromScratch.js"></script>
</head>

<body>
<canvas id="gl-canvas" width="512" height="512">
  HTML5 Canvas not supported!
</canvas>
</body>
</html>

